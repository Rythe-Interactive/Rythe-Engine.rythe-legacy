////////////////
//  Lighting  //
////////////////
#if !defined(LIGHTING_INCL)
    #define LIGHTING_INCL
#endif

#define ENUM uint

#define point_light 0
#define directional_light 1
#define spot_light 2

struct Light
{
	ENUM type;			// 4	0
	float attenuation;	// 4	4
	float intensity;	// 4	8
	uint index;			// 4	12
	vec3 direction;		// 12	16
	float falloff;		// 4	28
	vec3 position;		// 12	32
	float angle;		// 4	44
	vec3 color;		    // 12	48
	float meta;		    // 4	60
};

struct Material
{
    mat3 tbn;
    float height;
    vec4 albedo;
 	vec3 normal;
 	float metallic;
	vec3 emissive;
    float roughness;
    float dielectric;
 	float ambientOcclusion;
    vec2 texcoords;
    vec3 F0;
};

#include <math_ext.shinc>

float Attenuation(vec3 worldPosition, vec3 lightPosition, float attenuationRadius, float lightIntensity)
{
	float sqrlightDistance = length2(lightPosition - worldPosition);
	float attenuation = pow(max(1.0 - (sqrlightDistance / (attenuationRadius * attenuationRadius)), 0.0), 2);
	return attenuation * lightIntensity;
}

vec3 fresnelSchlick(float halfwayDotView, vec3 F0)
{
    return mix(pow(1.0 - halfwayDotView, 5.0).xxx, ones.rgb, F0);
}

vec3 Fresnel0(vec4 albedo, float metallic, float dielectric)
{
    return mix(dielectric.xxx, albedo.rgb, metallic.xxx);
}

vec3 Fresnel0(vec4 albedo, float metallic)
{
    return mix(vec3(0.04), albedo.rgb, metallic.xxx);
}

float DistributionGGX(float normalDotHalfway, float roughness)
{
    float a      = roughness * roughness;
    float a2     = a * a;
    float NdotH2 = normalDotHalfway * normalDotHalfway;

    float denom = ((NdotH2 * (a2 - 1.0)) + 1.0);
	denom = pi * denom * denom;

    return safeDiv(a2, denom);
}

float GeometrySchlickGGX(float cosTheta, float roughness)
{
    float r = roughness + 1.0;
    float k = (r * r) * 0.125;

    float denom = (cosTheta * (1.0 - k)) + k;

    return safeDiv(cosTheta, denom);
}

float GeometrySmith(float normalDotView, float normalDotLight, float roughness)
{
    float ggx2  = GeometrySchlickGGX(normalDotView, roughness);
    float ggx1  = GeometrySchlickGGX(normalDotLight, roughness);

    return ggx1 * ggx2;
}

vec3 CookTorranceBRDF(float normalDistribution, vec3 fresnelReflection, float geometryShadowing, float normalDotLight, float normalDotView)
{
    return safeDiv(normalDistribution * fresnelReflection * geometryShadowing, 4.0  * normalDotLight * normalDotView);
}

vec3 LambertianDiffuse(vec3 kS, vec3 albedo, float metallic)
{
    vec3 kD = (vec3(1.0) - kS) * (1.0 - metallic);
    return kD * albedo * one_over_pi;
}

float ambientIntensity;

vec3 CalculateLight(Light light, Camera camera, Material material, vec3 worldPosition)
{
    vec3 lightDirection;
    float intensity;
    switch(light.type)
    {
        case point_light:
            lightDirection = normalize(light.position - worldPosition);
            intensity = light.intensity;
            break;
        case directional_light:
            lightDirection = light.direction;
            intensity = light.intensity;
            break;
        case spot_light:
            lightDirection = normalize(light.position - worldPosition);
            intensity = pow(max(safeDiv(dot(normalize(light.direction), lightDirection) - cos(light.angle*2.0), (1.0 - cos(light.angle*2.0))), 0.0), light.falloff) * light.intensity;
            break;
        default:
            lightDirection = vec3(0, 1, 0);
            intensity = light.intensity;
            break;
    }

    ambientIntensity += (light.intensity * 1.5) - (intensity * 0.5);

    float attenuation = Attenuation(worldPosition, light.position, light.attenuation, intensity);
    if(attenuation <= 0)
        return vec3(0);

    vec3 radiance = light.color * attenuation;

    vec3 viewDir = normalize(camera.toView);

	// Microfacet normal that will reflect the incoming light into the viewing direction.
	// Technically only applies if the nanogeometry is perfectly smooth, but due to the inherent
	// inaccuracy of working with fragments as the smallest size of measurement we can ignore
	// nanogeometry for now.
	vec3 halfwayVector = normalize(lightDirection + viewDir);

    float halfwayDotView = dot01(halfwayVector, viewDir);
    float normalDotHalfway = dot01(material.normal, halfwayVector);
    float normalDotView = dot01(material.normal, viewDir);
    float normalDotLight = dot01(material.normal, lightDirection);

    float roughness = pow(material.roughness, 0.5);
    float metallic = pow(material.metallic, 0.5);

	// cook-torrance brdf
	vec3 fresnelReflection = fresnelSchlick(mix(halfwayDotView, 1.0, roughness), material.F0);
	float normalDistribution = DistributionGGX(normalDotHalfway, roughness);
	float geometryShadowing = GeometrySmith(normalDotView, normalDotLight, roughness);
    vec3 specular = CookTorranceBRDF(normalDistribution, fresnelReflection, geometryShadowing, normalDotLight, normalDotView);

    vec3 diffuse = LambertianDiffuse(fresnelReflection, material.albedo.rgb, metallic);

    return max((diffuse + specular) * radiance * normalDotLight.xxx, vec3(0));
}

vec3 GetAmbientLight(float ambientOcclusion, vec3 albedo)
{
    return (pow(ambientIntensity, 1.1) * 0.0001).xxx * ambientOcclusion.xxx * albedo;
}

#if !defined(NO_INPUT)
    #include <lighting_input.shinc>
#endif
